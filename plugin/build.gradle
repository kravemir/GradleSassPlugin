buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.0"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'java-gradle-plugin'
apply plugin: "com.gradle.plugin-publish"

apply from: "${projectDir}/../gradle/publishing.gradle"

group 'org.kravemir.gradle.sass'
version '1.2.4'

sourceCompatibility = 1.8

ext {
    publishingEnabled = project.hasProperty("enablePublishing")
    sonatypeUsername = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : ''
}


repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'com.vaadin:vaadin-sass-compiler:0.9.13'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.4'
    testCompile gradleTestKit()
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

pluginBundle {
    //website = 'http://kravemir.org/projects/gradle/sass-compiler-plugin'
    website = 'https://github.com/kravemir/GradleSassPlugin'
    vcsUrl = 'https://github.com/kravemir/GradleSassPlugin'
    description = 'Sass Plugin with support for multiple build configurations within project'
    tags = ['sass', 'scss', 'kravemir']
}

gradlePlugin {
    plugins {
        sass {
            id = 'org.kravemir.gradle.sass'
            displayName = project.getProperty('artifactName')
            description = project.getProperty('artifactDescription')
            implementationClass = 'org.kravemir.gradle.sass.SassPlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            name "sonatypeSnapshots"
            url 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username project.sonatypeUsername
                password "-- not specified --"
            }
        }
        maven {
            name "sonatypeStaging"
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username project.sonatypeUsername
                password "-- not specified --"
            }
        }
    }
}

if (publishingEnabled) {
    afterEvaluate {
        publishing.publications {
            pluginMaven {
                artifact sourceJar
                artifact javadocJar

                project.ext.fillPom(pom, project.getProperty('artifactName'), project.getProperty('artifactDescription'))
            }

            sassPluginMarkerMaven {
                project.ext.fillPom(pom, project.getProperty('pluginmarker.artifactName'), project.getProperty('pluginmarker.artifactDescription'))
            }
        }

        signing {
            publishing.publications.all { publication ->
                sign publication
                required { !gradle.taskGraph.allTasks.findAll { it.name ==~ /.*Sonatype.*/ }.isEmpty() }
            }
        }
    }
}
